// Scalar Multiplication for TOPRF:
// - mul(f, x) stands for the multiplication of point x with factors f
// - f is a multiset of factors
// - Combination of T-OPRF responses is modelled in rule combine
// - Rules add/remove/extract model multiplication with adversarially known factors (or its inverses)

functions:
    mul/2 // scalar multiplication


rule mul_down[no_derivcheck, color=#d66f7c]:
    [ In(mul(f, x)) ]-->[ !MulD(mul(f, x)) ]

rule mul_coerce[no_derivcheck, color=#d66f7c]: // similar to coerce rule (KD -> KU)
    [ !MulD(mul(f, x)) ]--[ Coerce(mul(f, x)) ]->[ !MulU(mul(f, x)) ]

rule mul_up[no_derivcheck, color=#d66f7c]:
    [ !MulU(mul(f, x)) ]-->[ Out(mul(f, x)) ]

// Construction Rules
rule combine[no_derivcheck, color=#d66f7c]: // [a]x, [b]x -> [a*b]x
    [ !MulU(mul(a, x)), !MulU(mul(b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ !MulU(mul(a ++ b, x)) ]

rule add[no_derivcheck, color=#d66f7c]: // a, [b]x -> [a*b]x
    [ In(a), !MulU(mul(b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ !MulU(mul(a ++ b, x)) ]

// Deconstruction Rules
rule remove[no_derivcheck, color=#d66f7c]: // a, [a*b]x -> [b]x
    [ In(a), !MulD(mul(a ++ b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ !MulD(mul(b, x)) ]

rule extract[no_derivcheck, color=#d66f7c]: // a, [a]x -> x
    [ In(a), !MulD(mul(a, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ Out(x) ]