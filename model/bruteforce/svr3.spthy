theory SVR3

// Models a simpler underapproximation of the SVR3 protocol:
// - No key rotation is modelled
// - No Noise.ExportedAuthenticator
// - Signature instead of ZKP
// - Client reuses sid from registration phase
//   Justification: In the "real world", client needs to remember the sids to avoid sending signatures to the wrong servers.
//   In the real world, this is "public" information?

// Adversary capabilities:
// - Server compromise: Observe all the secrets that were stored on the server as well as send/receive messages on its behalf
// - Access to client identifiers due to compromise of authentication server
// - Interact with the servers on its own.
// - Access to Quantum Computer that solves DLOG.
// - Bruteforce Attacks on password.

begin

builtins: xor, hashing, symmetric-encryption, revealing-signing, multiset

predicates:
    NEQ(x, y) <=> not (x = y)
  , DISTINCT(x, y, z) <=> not (x = y | x = z | y = z)

functions:
  KDF/2

// provides: SCClientOut, SCClientIn, SCServerOut, SCServerIn
// uses: !Server, !Compromised
#include "src/securechannel.spthy"

// provides: function mul
// uses: !Factor
#include "src/toprf.spthy"

// provides: !HumanGenerated 
#include "src/bruteforce.spthy"

// Proof automation:
#include "./tactics"

// World Building

rule create_server:
    []
  --[]->
    [ !Server($sid) ]


// Adversary Capabilities

rule compromise_server[color=#d66f7c]:
    [ !Server($sid) ]
  --[ AdversaryAction()
    , !Compromised($sid) ]->
    [ !Compromised($sid) ]

rule solve_dlog[no_derivcheck, color=#d66f7c]: // [f]x -> f
    [ !Factor(f) ] // TODO: Add In(mul(g, x)) with f << g | f = g if necessary
  --[ AdversaryAction()
    , PQAdversaryAction() ]->
    [ Out(f) ]

// Additional adversary capabilities are in the imported files

// Protocol
// Initialisation of the client with password and secret to be protected.
// Each client can store and register multiple secrets. Nondeterminism ensures that it is possible to get the right responses back from the servers.
rule client_initialise[color=#77b58d]:
    [ Fr(~pw), Fr(~secret) ]
  --[
      _restrict(NEQ($cid, 'adv')) // The adversary may contact the servers, but can't operate as its own client. Without this restriction, it would be as if the adversary could compromise 1 client.
    ]->
    [ Client_0($cid, ~pw, ~secret), !HumanGenerated(~pw) ]

// Client Registration    
rule client_register[color=#77b58d]:
    let
        koprf = ~koprf1 ++ ~koprf2 ++ ~koprf3
        s_enc = ~s_enc1 XOR ~s_enc2 XOR ~s_enc3
        kauth = h(<~pw, mul(koprf, h(~pw))>)
        kenc = KDF(s_enc, kauth)
        ksign1 = KDF(kauth, '1')
        ksign2 = KDF(kauth, '2')
        ksign3 = KDF(kauth, '3')
        kvfy1 = pk(ksign1)
        kvfy2 = pk(ksign2)
        kvfy3 = pk(ksign3)
        // why not AEAD: AEAD doesn't make the adversary weaker because he can still derive the same messages.
        // In a sense, the adversary works together with Tamarin and may try infinitely many keys. Tamarin will be telling us if the adversary "won".
        // Furthermore, this only weakens the client, as he doesn't work with Tamarin and no longer knows if decryption was successful.
        encrypted_secret = senc(~secret, kenc)  
    in
    [ Client_0($cid, ~pw, ~secret)
    , Fr(~koprf1), Fr(~koprf2), Fr(~koprf3)
    , Fr(~s_enc1), Fr(~s_enc2), Fr(~s_enc3)
    , !Server($sid1), !Server($sid2), !Server($sid3) ]
  --[ _restrict(DISTINCT($sid1, $sid2, $sid3))
    , ClientRegister($cid, ~pw, ~secret)
    , SourceC(~s_enc1), SourceC(~s_enc2), SourceC(~s_enc3)
    , SourceP(h(~pw))
    , Secret('pw', ~pw, $cid, <$sid1, $sid2, $sid3>)
    , Secret('koprf', koprf, $cid, <$sid1, $sid2, $sid3>)
    , Secret('kauth', kauth, $cid, <$sid1, $sid2, $sid3>)
    , Secret('senc', s_enc, $cid, <$sid1, $sid2, $sid3>)
    , Secret('senc_share', ~s_enc1, $cid, $sid1)
    , Secret('senc_share', ~s_enc2, $cid, $sid2)
    , Secret('senc_share', ~s_enc3, $cid, $sid3)
    , Secret('kenc', kenc, $cid, <$sid1, $sid2, $sid3>)
    , Secret('secret', ~secret, $cid, <$sid1, $sid2, $sid3>) ]->
    [ Out(encrypted_secret) // Q: should this be annotated with $cid? IMO not needed as long as we don't strive for success guarantees
    //, !BruteforceCandidate(encrypted_secret)
    , SCClientOut($cid, $sid1, <$cid, ~koprf1, ~s_enc1, kvfy1>)
    , SCClientOut($cid, $sid2, <$cid, ~koprf2, ~s_enc2, kvfy2>)
    , SCClientOut($cid, $sid3, <$cid, ~koprf3, ~s_enc3, kvfy3>)
    , !Factor(~koprf1), !Factor(~koprf2), !Factor(~koprf3)
    //, !BruteforceCandidate(kfv1), !BruteforceCandidate(kfv2), !BruteforceCandidate(kfv3) // the other elements of the tuples definitely do not contain the password
    , !BruteforceCandidate(kauth)
    , !Client_1($cid, ~pw, $sid1, $sid2, $sid3) // forget the secret
    ]

// Server Registration
rule server_register[color=#7793b5]:
    [ !Server($sid)
      // Note: $client and $cid don't necessarily match because the client isn't authenticated.
      // Note 2: ~koprf_share is fresh because theres no reason to allow for arbitrary factors
    , SCServerIn($client, $sid, <$cid, ~koprf_share, s_enc_share, kvfy>)
    ]
  --[ ServerAction() ]->
    [ !Server_1($sid, $cid, ~koprf_share, s_enc_share, kvfy) ]


// Client Reconstruction (Phase 1)
rule client_reconstruct_phase1[color=#77b58d]:
    [ !Client_1($cid, ~pw, $sid1, $sid2, $sid3)
    , Fr(~b) ]
  --[ ClientReconstructPhase1($cid, ~pw)
    , BlindingFactor(~b)
    , !MulTerm(mul(~b, h(~pw))) ]->
    [ SCClientOut($cid, $sid1, <$cid, mul(~b, h(~pw))>)
    , SCClientOut($cid, $sid2, <$cid, mul(~b, h(~pw))>)
    , SCClientOut($cid, $sid3, <$cid, mul(~b, h(~pw))>)
    , !BruteforceCandidate(mul(~b, h(~pw)))
    , Client_2($cid, ~pw, $sid1, $sid2, $sid3, ~b)
    , !Factor(~b) ]

// PATTERN MATCHING FOR:
// - scalar multiplication with koprf_share
rule server_reconstruct_phase1[no_derivcheck, color=#7793b5]:
    let
      bhpw = mul(factors, hpw)
    in
    [ !Server_1($sid, $cid, ~koprf_share, s_enc_share, kvfy)
    , SCServerIn($client, $sid, <$cid, bhpw>) // Note: $client and $cid don't necessarily match because the client isn't authenticated.
    ]
  --[ ServerAction()
    , ServerReconstructPhase1($sid)
    , SourceC(mul(factors ++ ~koprf_share, hpw))
    , SourcesPoint(hpw) ]->
    [ SCServerOut($sid, $client, mul(factors ++ ~koprf_share, hpw))
    , Server_2($sid, $cid, s_enc_share, kvfy, bhpw)
    , !Factor(~koprf_share) ]


// PATTERN MATCHING TO:
// - deblind with ~b (uses commutativity)
// - combine the shares
rule client_reconstruct_phase2[no_derivcheck, color=#77b58d]:
    let
        koprf = koprf1 ++ koprf2 ++ koprf3
        kauth = h(<~pw, mul(koprf, hpw)>)
        ksign1 = KDF(kauth, '1')
        ksign2 = KDF(kauth, '2')
        ksign3 = KDF(kauth, '3')
        B = mul(~b, h(~pw)) // was already produced once, so no need to add it as a source here
        c = h(B) // Q: Think about how to model Noise.ExportedAuthenticator and if it even needs to be modelled!
        sig1 = revealSign(c, ksign1)
        sig2 = revealSign(c, ksign2)
        sig3 = revealSign(c, ksign3)
    in 
    [ Client_2($cid, ~pw, $sid1, $sid2, $sid3, ~b)
    , SCClientIn($sid1, $cid, mul(~b ++ koprf1, hpw))
    , SCClientIn($sid2, $cid, mul(~b ++ koprf2, hpw))
    , SCClientIn($sid3, $cid, mul(~b ++ koprf3, hpw)) ]
  --[ ClientReconstructPhase2($cid, ~pw) ]->
    [ SCClientOut($cid, $sid1, sig1)
    , SCClientOut($cid, $sid2, sig2)
    , SCClientOut($cid, $sid3, sig3)
    , Client_3($cid, ~pw, kauth, $sid1, $sid2, $sid3) ]
    
rule server_reconstruct_phase2[color=#7793b5]:
    let
        c = h(bhpw) // Change if Noise.ExportedAuthenticator is modelled!
    in
    [ Server_2($sid, $cid, s_enc_share, kvfy, bhpw)
    , SCServerIn($client, $sid, sig) // Note: $client and $cid don't necessarily match because the client isn't authenticated.
    ]
  --[ _restrict(revealVerify(sig, c, kvfy) = true)
    , ServerAction() ]->
    [ SCServerOut($sid, $client, s_enc_share) ]


rule client_reconstruct_finish[color=#77b58d]:
    let
        s_enc = s_enc1 XOR s_enc2 XOR s_enc3
        kenc = KDF(s_enc, kauth)
        secret = sdec(encrypted_secret, kenc)
    in
    [ Client_3($cid, ~pw, kauth, $sid1, $sid2, $sid3)
    , SCClientIn($sid1, $cid, s_enc1)
    , SCClientIn($sid2, $cid, s_enc2)
    , SCClientIn($sid3, $cid, s_enc3)
    , In(encrypted_secret) ]
  --[ ClientReconstructFinish($cid, ~pw, secret) ]->
    []


restriction servers_offline:
    "All #i #j. PQAdversaryAction()@#i & ServerAction()@#j ==> #j < #i"


// Sources lemma:

// There are 22 partial deconstructions because of adversary_recv.
// Tamarin believes any msg can be derived using that rule.
// But:
// - Out comes from !SCServerKnows
// - !SCServerKnows comes from SCServerOut
// - SCServerOut comes from various protocol rules

// SourcesPoint is like an invariant that says: The point is either h(pw) or the adversary knows it.
lemma types[sources,heuristic={sources_lemma},reuse]: // autoprove
    "(All msg #i.
      SourcesAdversaryRecv(msg)@#i
      ==>
      ( (Ex #j. SourceC(msg)@#j & #j < #i)
      | (Ex #j. !KU(msg)@#j & #j < #i)
      )
    ) & (All x #i.
      SourcesPoint(x)@#i
      ==>
      ( (Ex #j. SourceP(x)@#j & #j < #i)
      | (Ex #j. !KU(x)@#j & #j < #i)
      )
    )"

lemma executability[hide_lemma=types]: // sanity check for protocol model
    exists-trace "
      // adversary doesn't do anything
      not (Ex #i. AdversaryAction()@#i)
      &
      // client only registers once
      (All cid pw1 pw2 secret1 secret2 #i #j. ClientRegister(cid, pw1, secret1)@#i & ClientRegister(cid, pw2, secret2)@#j ==> #i = #j)
      &
      // client only reconstructs once
      (All cid pw1 pw2 #i #j. ClientReconstructPhase1(cid, pw1)@#i & ClientReconstructPhase1(cid, pw2)@#j ==> #i = #j)
      &
      // servers only run once
      (All sid #i #j. ServerReconstructPhase1(sid)@#i & ServerReconstructPhase1(sid)@#j ==> #i = #j)
      &
      Ex cid pw secret #i #j #k #l.
      ( ClientRegister(cid, pw, secret)@#i 
      & ClientReconstructPhase1(cid, pw)@#j
      & ClientReconstructPhase2(cid, pw)@#k
      & ClientReconstructFinish(cid, pw, secret)@#l
      )
    "


lemma post_quantum_attack[hide_lemma=types]: // sanity check for adversary capabilities
    exists-trace "
      // client only registers once
      (All cid pw1 pw2 secret1 secret2 #i #j. ClientRegister(cid, pw1, secret1)@#i & ClientRegister(cid, pw2, secret2)@#j ==> #i = #j)
      &
      // client only reconstructs once
      (All cid pw1 pw2 #i #j. ClientReconstructPhase1(cid, pw1)@#i & ClientReconstructPhase1(cid, pw2)@#j ==> #i = #j)
      & 
      // attack
      Ex cid pw koprf kauth sid1 sid2 sid3 #i #j #k #l #m #n.
          Secret('pw', pw, cid, <sid1, sid2, sid3>)@#i & !KU(pw)@#j
        & Secret('koprf', koprf, cid, <sid1, sid2, sid3>)@#k & !KU(koprf)@#l
        & Secret('kauth', kauth, cid, <sid1, sid2, sid3>)@#m & !KU(kauth)@#n
        & not (Ex #o #p #q. !Compromised(sid1)@#o & !Compromised(sid2)@#p & !Compromised(sid3)@#q)
    "

// MAIN SECRECY GOAL


lemma mul_terms[reuse,hide_lemma=types]: // The only way the adversary can get rid of the blinding factor of the client is with a quantum computer
    "All f x #i #j.
      !KU(mul(f, x))@#i & SourceP(x)@#j
      ==> (Ex b #k. (b << f | b = f) & BlindingFactor(b)@#k)
      | (Ex #k. PQAdversaryAction()@#k & #k < #i)"

lemma pw_pre_quantum_secrecy[reuse,heuristic={pw_pre_quantum_secrecy},hide_lemma=types]:
    "All pw cid sid1 sid2 sid3 #i. Secret('pw', pw, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(pw)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)
      | (Ex #j #k. KU(pw)@#j & PQAdversaryAction()@#k & #k < #j)"

lemma senc_share_secrecy[reuse,heuristic={senc_share_secrecy},hide_lemma=types]:
    "All s_enc_share cid sid #i. Secret('senc_share', s_enc_share, cid, sid)@#i
      ==> (not Ex #j. KU(s_enc_share)@#j)
      | (Ex #j. !Compromised(sid)@#j)"

lemma senc_secrecy[reuse,heuristic={senc_secrecy},hide_lemma=pw_pre_quantum_secrecy]:
    "All s_enc cid sid1 sid2 sid3 #i. Secret('senc', s_enc, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(s_enc)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)"

lemma secrecy[hide_lemma=types,hide_lemma=pw_pre_quantum_secrecy,hide_lemma=senc_share_secrecy]:
    "All secret cid sid1 sid2 sid3 #i. Secret('secret', secret, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(secret)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)"


// LEMMAS FOR ADDITIONAL INVESTIGATION
lemma koprf_pre_quantum_secrecy[hide_lemma=types,hide_lemma=pw_pre_quantum_secrecy,hide_lemma=senc_share_secrecy,hide_lemma=senc_secrecy]:
    "All koprf cid sid1 sid2 sid3 #i. Secret('koprf', koprf, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(koprf)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)
      | (Ex #j #k. KU(koprf)@#j & PQAdversaryAction()@#k & #k < #j)"

lemma kauth_pre_quantum_secrecy[hide_lemma=types,hide_lemma=senc_share_secrecy,hide_lemma=senc_secrecy]:
    "All kauth cid sid1 sid2 sid3 #i. Secret('kauth', kauth, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(kauth)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)
      | (Ex #j #k. KU(kauth)@#j & PQAdversaryAction()@#k & #k < #j)"

lemma kenc_secrecy[hide_lemma=types,hide_lemma=pw_pre_quantum_secrecy,hide_lemma=senc_share_secrecy]:
    "All kenc cid sid1 sid2 sid3 #i. Secret('kenc', kenc, cid, <sid1, sid2, sid3>)@#i
      ==> (not Ex #j. KU(kenc)@#j)
      | (Ex #j #k #l. !Compromised(sid1)@#j & !Compromised(sid2)@#k & !Compromised(sid3)@#l)"

end