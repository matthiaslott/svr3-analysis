// Scalar Multiplication for TOPRF:
// - mul(f, x) stands for the multiplication of point x with factors f
// - f is a multiset of factors
// - Combination of T-OPRF responses is modelled in rule combine
// - Rules add/remove/extract model multiplication with adversarially known factors (or its inverses)

functions:
    mul/2 // scalar multiplication

// Scalar Multiplication & DLOG
rule combine[no_derivcheck, color=#d66f7c]: // [a]x, [b]x -> [a*b]x
    [ In(mul(a, x)), In(mul(b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ Out(mul(a ++ b, x)) ]

rule add[no_derivcheck, color=#d66f7c]: // a, [b]x -> [a*b]x
    [ !Factor(a), In(a), In(mul(b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ Out(mul(a ++ b, x)) ]

rule remove[no_derivcheck, color=#d66f7c]: // a, [a*b]x -> [b]x
    [ !Factor(a), In(a), In(mul(a ++ b, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ Out(mul(b, x)) ]

rule extract[no_derivcheck, color=#d66f7c]: // a, [a]x -> x
    [ In(a), In(mul(a, x)) ]
  --[ AdversaryAction()
    , SourcesPoint(x) ]->
    [ Out(x) ]
